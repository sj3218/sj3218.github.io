---
layout: page
name: 3D ê·¸ë˜í”½ìŠ¤ ì—”ì§„
tools: [C++, OpenGL, Custom Engine, 3D]
image: "/assets/images/background/graphics.JPG"
---

# 3D Graphics Engine

<br>
{% include elements/video.html id="gCqtzcakhlA" %}

<br>

<div style="display: flex; gap: 20px;">
  <div style="background-color: #2c2c2c; padding: 20px; border-radius: 8px; color: white; width: 50%;">
    <h2>Description</h2><br>
    <p>
      ì´ í”„ë¡œì íŠ¸ëŠ” Modern GPU êµ¬ì¡°ë¥¼ ì´í•´í•˜ê³  OpenGLì„ ì´ìš©í•œ ê·¸ë˜í”½ìŠ¤ ì—”ì§„ì„ ê°œë°œí•˜ê¸° ìœ„í•œ ëª©ì ìœ¼ë¡œ ì§„í–‰ë˜ì—ˆìŠµë‹ˆë‹¤. ë©”ì¸ ì˜¤ë¸Œì íŠ¸ë¥¼ ì¤‘ì‹¬ìœ¼ë¡œ ë¹› êµ¬ì²´ë“¤ì´ íšŒì „í•˜ëŠ” ì”¬ì„ êµ¬í˜„í–ˆìŠµë‹ˆë‹¤. 
    </p>
  </div>
  <div style="background-color: #2c2c2c; padding: 20px; border-radius: 8px; color: white; width: 50%;">
    <h2>Project Info</h2><br>
    <p>ğŸ‘¥ íŒ€ ê·œëª¨: ì†”ë¡œ í”„ë¡œì íŠ¸</p>
    <p>â³ ê°œë°œ ê¸°ê°„: 2021.08 ~ 2022.04</p>
    <p>ğŸ› ï¸ Engine: ìì²´ ì—”ì§„(C++, OpenGL, ImGui, GLSL)</p>
    <p>âš™ï¸ Git hub: <button onclick="window.location.href='https://github.com/sj3218/Graphics-Project-3';">ë§í¬ ì´ë™</button></p>
  </div>
</div>

<br>


### Shading ì•Œê³ ë¦¬ì¦˜

###### ì´ í”„ë¡œì íŠ¸ì—ì„œëŠ” Phong Lighting, Phong Shading, Blinn Shading ì•Œê³ ë¦¬ì¦˜ì„ êµ¬í˜„í•˜ì˜€ìŠµë‹ˆë‹¤. 
- ###### **Phong Lighting**ì€ vertex shaderì—ì„œ ê³„ì‚°ëœ ë°˜ì‚¬ ë²¡í„°ë¥¼ ì‚¬ìš©í•˜ì—¬ diffuseì™€ specular ìƒ‰ì„ ê³„ì‚°í•©ë‹ˆë‹¤.
- ###### **Phong Shading**ì€ Phong Lightingê³¼ ìœ ì‚¬í•˜ì§€ë§Œ, ë¹› ê³„ì‚°ì„ fragment shaderì—ì„œ ìˆ˜í–‰í•˜ëŠ” ë°©ì‹ì…ë‹ˆë‹¤. 


<details>
<summary>Phong Shading.frag</summary>
<div markdown="1" style="padding-left:20px;">

```glsl
#version 450 core

#define PI 4.0f * atan(1.0f)

in vec3 fragPosition;
in vec3 outNormal;
in vec2 texCoord;

out vec4 fragColor;

struct lightInformation
{
    int lightType; // dir, point, spot
    float innerAngle;
    float outerAngle;
    float fallOff;
    vec3 ambientColor;
    vec3 diffuseColor;
    vec3 specularColor;
    vec3 lightPosition;
    vec3 direction;
};

layout (std140, binding =0) uniform global
{
    int numbLights;
    float far;
    float near;
    vec3 attenuation;
    vec3 fogColor;
    vec3 globalAmbientColor;
};

layout (std140, binding =1) uniform light
{
    lightInformation lights[16];
};

uniform float ambientStrength;
uniform bool isTexture;
uniform sampler2D diffuseTexture;
uniform sampler2D specularTexture;
uniform vec3 ambientMaterial;
uniform vec3 emissive;
uniform vec3 cameraPosition;
uniform vec3 color;

uniform bool calculateUV_GPU;
uniform int projectionMode;
uniform bool entities;

uniform mat4 transform;
uniform mat4 cameraMatrix;
uniform mat4 ndcMatrix;

vec3 CalculateDirectionalLight(int index, vec3 normal, vec3 view, vec2 tex)
{
    int Ns = 64;
    vec3 lightDir = normalize(-lights[index].direction);
    vec3 reflectDir = 2*dot(normal, lightDir)*normal - lightDir;

    float diffuseValue = max(dot(normal, lightDir),0.f);
    float specularValue = pow(max(dot(view, reflectDir), 0.f), Ns);

    vec3 ambient = lights[index].ambientColor * ambientMaterial * ambientStrength;
    vec3 diffuse = lights[index].diffuseColor * diffuseValue;
    vec3 specular = lights[index].specularColor * specularValue;

    if(isTexture)
    {
        diffuse *= vec3(texture(diffuseTexture, tex).rgb);
        specular *= vec3(texture(specularTexture, tex).rgb);
    }

    return ambient + diffuse + specular;
}

vec3 CalculatePointLight(int index, vec3 normal, vec3 view, vec3 pos, vec2 tex)
{
    float Ns = 64;

    vec3 lightDir = lights[index].lightPosition - pos;
    float lightDirLength = length(lightDir);
    
    float attenuationValue = min(1/(attenuation.x + attenuation.y * lightDirLength + attenuation.z * lightDirLength*lightDirLength), 1.f);

    lightDir = normalize(lightDir);

    vec3 reflectDir = 2*dot(normal, lightDir)*normal - lightDir;

    float diffuseValue = max(dot(normal, lightDir), 0.f);
    float specularValue = pow(max(dot(view, reflectDir), 0.f), Ns);

    vec3 ambient = lights[index].ambientColor * ambientMaterial * ambientStrength;
    vec3 diffuse = lights[index].diffuseColor * diffuseValue;
    vec3 specular = lights[index].specularColor * specularValue;

    if(isTexture)
    {
        diffuse *= vec3(texture(diffuseTexture, tex).rgb);
        specular *= vec3(texture(specularTexture, tex).rgb);
    }

    return (ambient + diffuse + specular) * attenuationValue;
}

vec3 CalculateSpotLight(int index, vec3 normal, vec3 view, vec3 pos, vec2 tex)
{
    int Ns = 64;
    vec3 lightDir = lights[index].lightPosition- pos;
    float lightDirLength = length(lightDir);

    float attenuationValue = min(1/(attenuation.x + attenuation.y * lightDirLength + attenuation.z * lightDirLength*lightDirLength), 1.f);

    lightDir = normalize(lightDir);

    vec3 reflectDir = 2*dot(normal, lightDir)*normal - lightDir;

    float diffuseValue = max(dot(normal, lightDir), 0.f);
    float specularValue = pow(max(dot(view, reflectDir), 0.f), Ns);

    float cosAlpha = dot(lightDir, normalize(-lights[index].direction));

    float spotLightEffect =0;
    if(cosAlpha < cos(lights[index].outerAngle))
    {
        spotLightEffect = 0.f;
    }
    else if (cosAlpha > cos(lights[index].innerAngle))
    {
        spotLightEffect = 1.f;
    }
    else
    {
        spotLightEffect = pow((cosAlpha - cos(lights[index].outerAngle))/(cos(lights[index].innerAngle)-cos(lights[index].outerAngle)), lights[index].fallOff);
    }

    vec3 ambient = lights[index].ambientColor * ambientMaterial * ambientStrength;
    vec3 diffuse = lights[index].diffuseColor * diffuseValue;
    vec3 specular = lights[index].specularColor * specularValue;

    if(isTexture)
    {
        diffuse *= vec3(texture(diffuseTexture, tex).rgb);
        specular *= vec3(texture(specularTexture, tex).rgb);
    }


    return attenuationValue*(ambient + spotLightEffect*(diffuse + specular));
}

void main()
{
    vec2 texture_coordinate;
    if(calculateUV_GPU)
    {
        float u = 0.f;
        float v = 0.f;
        float theta = 0.f;
        float phi = 0.f;
        
        vec3 calculatePosition = fragPosition;

        if(entities)
        {
            calculatePosition = normalize(outNormal);
        }
        
        if(projectionMode == 1) // cylindrical uv
        {
            theta = atan(calculatePosition.z, calculatePosition.x);
            theta += PI;
            
            u = theta/(2.f*PI);
            v = (calculatePosition.y + 1.0f) *0.5f;
        }
        else if(projectionMode == 2) // spherical uv
        {
            theta = atan(calculatePosition.z , calculatePosition.x);
            theta += PI;

            phi = acos(calculatePosition.y/ length(calculatePosition));
            
            u = theta/(2.f*PI);
            v = 1.f - (phi / PI);
        }
        else if(projectionMode == 3) // cube uv
        {
            
            float x = calculatePosition.x;
            float y = calculatePosition.y;
            float z = calculatePosition.z;

            float absX = abs(x);
            float absY = abs(y);
            float absZ = abs(z);

            int isXPositive = x > 0 ? 1 : 0;
            int isYPositive = y > 0 ? 1 :0;
            int isZPositive = z > 0 ? 1 :0;

            float maxAxis = 0.f;
            float uc = 0.f;
            float vc = 0.f;
    
            // POSITIVE X
            if (bool(isXPositive) && (absX >= absY) && (absX >= absZ))
            {
                maxAxis = absX;
                uc = -z;
                vc = y;
            }
            // NEGATIVE X
            else if (!bool(isXPositive) && absX >= absY && absX >= absZ)
            {
                maxAxis = absX;
                uc = z;
                vc = y;
            }

            // POSITIVE Y
            else if (bool(isYPositive) && absY >= absX && absY >= absZ)
            {
                maxAxis = absY;
                uc = x;
                vc = -z;
            }

            // NEGATIVE Y
            else if (!bool(isYPositive) && absY >= absX && absY >= absZ)
            {
                maxAxis = absY;
                uc = x;
                vc = z;
            }

            // POSITIVE Z
            else if (bool(isZPositive) && absZ >= absX && absZ >= absY)
            {
                maxAxis = absZ;
                uc = x;
                vc = y;
            }

            // NEGATIVE Z
            else if (!bool(isZPositive) && absZ >= absX && absZ >= absY)
            {
                maxAxis = absZ;
                uc = -x;
                vc = y;
            }

            // Convert range from -1 to 1 to 0 to 1
            u = 0.5f * (uc / maxAxis + 1.0f);
            v = 0.5f * (vc / maxAxis + 1.0f);
        }
        texture_coordinate.x = u;
        texture_coordinate.y = v;
    }
    else
    {
        texture_coordinate = texCoord;
    }
    
    vec3 result = emissive + globalAmbientColor* ambientStrength;
    vec3 normalVector = normalize(outNormal);

    vec3 viewVector = cameraPosition - fragPosition;
    float viewVectorLength = length(viewVector);

    viewVector = normalize(viewVector);

    for(int i =0; i< numbLights; ++i)
    {
        if(lights[i].lightType == 0)
        {
            result += CalculatePointLight(i, normalVector, viewVector, fragPosition,texture_coordinate);
        }
        else if(lights[i].lightType == 1)
        {
            result += CalculateDirectionalLight(i, normalVector, viewVector, texture_coordinate);
        }
        else if(lights[i].lightType == 2)
        {
            result += CalculateSpotLight(i, normalVector, viewVector, fragPosition, texture_coordinate);
        }
    }

    //fog
    float tempNear = near;
    if(near > far)
    {
        tempNear = far;
    }

    float fogValue = (far - viewVectorLength) / (far - tempNear);
    fogValue = min(fogValue, 1.f);
    fogValue = max(fogValue, 0.f);
    

    vec3 IFinal = fogValue* result + (1-fogValue) * fogColor;
    IFinal = vec3(min(IFinal.x, 1.f), min(IFinal.y, 1.f), min(IFinal.z, 1));
    
    fragColor = vec4(IFinal, 1.f);
    
}
```
</div>
</details> 

- ###### **Blinn Shading**ì—ì„œëŠ” ë°˜ì‚¬ ë²¡í„°ë¥¼ ì§ì ‘ ê³„ì‚°í•˜ëŠ” ëŒ€ì‹ , ë¹› ë²¡í„°ì™€ ì‹œì  ë²¡í„°ë¥¼ ë”í•´ ì´ë¥¼ normalizeì‹œí‚¨ ë²¡í„°ë¥¼ ì‚¬ìš©í•˜ì—¬ ë°˜ì‚¬ íš¨ê³¼ë¥¼ ì²˜ë¦¬í–ˆìŠµë‹ˆë‹¤.

```glsl
vec3 reflectDir = (lightDir + view);
reflectDir = normalize(reflectDir);
```

###### ê²°ê³¼ì ìœ¼ë¡œ **Blinn Shading**ì€ ë‹¤ë¥¸ ì‰ì´ë”©ê³¼ ë¹„ìŠ·í•œ ê²°ê³¼ë¥¼ ì œê³µí•˜ê³  ë‹¤ë¥¸ ë°˜ì‚¬ ë²¡í„° ê³„ì‚°í•˜ëŠ”ë° ë¹„ìš©ì´ ë¹„ì‹¼ ë‹¤ë¥¸ ì‰ì´ë”© ì•Œê³ ë¦¬ì¦˜ ë³´ë‹¤ ë” íš¨ìœ¨ì ì¸ ê²ƒì„ ì•Œ ìˆ˜ ìˆì—ˆìŠµë‹ˆë‹¤.


<br>

### í™˜ê²½ ë§µí•‘

###### ì´ í”„ë¡œì íŠ¸ì—ì„œëŠ” Skyboxì™€ í™˜ê²½ë§µí•‘(ë°˜ì‚¬, êµ´ì ˆ)ì„ ë³´ì—¬ì¤ë‹ˆë‹¤. OpenGLì—ì„œ ì œê³µí•˜ëŠ” íë¸Œ ë§µí•‘ í•¨ìˆ˜ë¥¼ ì“°ì§€ ì•Šê³  ì§ì ‘ êµ¬í˜„ì„ í•´ë³´ë©° ì–´ë–¤ ì›ë¦¬ë¡œ ì‘ë™ì„ í•˜ëŠ”ì§€ ê³µë¶€í•˜ì˜€ìŠµë‹ˆë‹¤. FBOì— 6ê°œì˜ ë‹¤ë¥¸ í…ìŠ¤ì²˜ë“¤ì„ **skyTexture**ì— ì €ì¥í•˜ê³  CubeMapping í•¨ìˆ˜ì—ì„œ ì €ì¥ëœ í…ìŠ¤ì²˜ë“¤ì„ í™œìš©í•˜ì—¬ ë¬¼ì²´ì˜ í‘œë©´ì— ë°˜ì‚¬ ë° êµ´ì ˆ íš¨ê³¼ë¥¼ ì ìš©í•˜ê³ , ì´ë¥¼ í†µí•´ í˜„ì‹¤ê° ìˆëŠ” ì¥ë©´ì„ ì—°ì¶œ í•  ìˆ˜ ìˆì—ˆìŠµë‹ˆë‹¤.  
###### ë˜í•œ Hybrid Rendering ê¸°ë²•ì„ ì‚¬ìš©í•´ ëª¨ë“  ë¹›ë“¤ì„ forward ë Œë”ë§ ê¸°ë²•ì„ ì‚¬ìš©í•˜ì—¬ ë²„í¼ì— ì €ì¥í•˜ê³  í™˜ê²½ ë§µí•‘ì„ ë°›ëŠ” ë©”ì¸ ì˜¤ë¸Œì íŠ¸ì— deferred ë Œë”ë§ ê¸°ë²•ì„ ì‚¬ìš©í•´ì„œ ë Œë”ë§ ì²˜ë¦¬ í•˜ì˜€ìŠµë‹ˆë‹¤.


```glsl

uniform sampler2D skyTexture[6];
uniform float blending;
uniform int environmentType; //0 = reflection, 1 = refraction, 2 = combination

vec3 CubeMapping(vec3 pos)
{
    float x = pos.x;
    float y = pos.y;
    float z = pos.z;

    float absX = abs(x);
    float absY = abs(y);
    float absZ = abs(z);

    int isXPositive = x > 0 ? 1 : 0;
    int isYPositive = y > 0 ? 1 : 0;
    int isZPositive = z > 0 ? 1 : 0;

    float maxAxis =  0.f;
    vec2 uv = vec2(0.f);

    vec3 resultTexture = vec3(0.f);

    if((absX >= absY) && (absX >= absZ))
    {
        if(isXPositive == 1) // positive x // right
        {
            maxAxis = absX;

            uv.x = 0.5f * (z / maxAxis + 1.f);
            uv.y = 0.5f * (y / maxAxis + 1.f);

            resultTexture = texture(skyTexture[1], uv).rgb;
        }
        else if(isXPositive == 0) // negative x // left
        {
            maxAxis = absX;

            uv.x = 0.5f * (-z / maxAxis + 1.f);
            uv.y = 0.5f * (y / maxAxis + 1.f);

            resultTexture = texture(skyTexture[0], uv).rgb;
        }
    }
    else if((absY >= absX) && (absY >= absZ))
    {
        if(isYPositive == 1) //positive y // top
        {
            maxAxis = absY;
            
            uv.x = 0.5f * (-z / maxAxis + 1.f);
            uv.y = 0.5f * (x / maxAxis + 1.f);

            resultTexture = texture(skyTexture[3], uv).rgb;
        }
        else if(isYPositive == 0) // negative y // bottom
        {
            maxAxis = absY;
            
            uv.x = 0.5f * (z / maxAxis + 1.f);
            uv.y = 0.5f * (x / maxAxis + 1.f);

            resultTexture = texture(skyTexture[2], uv).rgb;
        }
    }
    else if((absZ >= absX) && (absZ >= absY))
    {
        if(isZPositive == 1) // positive z // front
        {
            maxAxis = absZ;
            
            uv.x = 0.5f * (-x / maxAxis + 1.f);
            uv.y = 0.5f * (y / maxAxis + 1.f);

            resultTexture = texture(skyTexture[5], uv).rgb;
        }
        else if(isZPositive == 0) //negative z // back
        {
            maxAxis = absZ;
            
            uv.x = 0.5f * (x / maxAxis + 1.f);
            uv.y = 0.5f * (y / maxAxis + 1.f);

            resultTexture = texture(skyTexture[4], uv).rgb;
        }
    }
    return resultTexture;
}
```
<br>
{% capture carousel_images %}
/assets/images/graphics_engine/graphics_environment/reflection.JPG
/assets/images/graphics_engine/graphics_environment/refraction.JPG
/assets/images/graphics_engine/graphics_environment/fresnel.JPG
/assets/images/graphics_engine/graphics_environment/chromatic_aberration.JPG
/assets/images/graphics_engine/graphics_environment/blending_reflection.JPG
{% endcapture %}


### Dynamics ë¹› êµ¬í˜„

###### Point, Directional, Spot ë¹›ì„ êµ¬í˜„í•˜ì˜€ìŠµë‹ˆë‹¤.

{% capture carousel_images %}
/assets/images/graphics_engine/point_light.JPG
/assets/images/graphics_engine/directional.JPG
/assets/images/graphics_engine/spot_light.JPG
{% endcapture %}